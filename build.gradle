plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.41" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.2.41" apply false
    id "org.springframework.boot" version "2.0.2.RELEASE" apply false
}

allprojects {
    group = "jp.gr.java_conf.stardiopside.ktlearn"
    version = "0.0.1-SNAPSHOT"
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "kotlin"
    apply plugin: "kotlin-spring"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    repositories {
        jcenter()
    }

    ext {
        mp3spiVersion = "1.9.5.4"
        jaadVersion = "0.8.5"
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
}

project(":sound-service") {
    dependencies {
        api "org.springframework.boot:spring-boot-starter"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }
}

project(":sound-console") {
    apply plugin: "org.springframework.boot"

    dependencies {
        implementation project(":sound-service")
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        runtimeOnly "com.googlecode.soundlibs:mp3spi:${mp3spiVersion}"
        runtimeOnly "net.sourceforge.jaadec:jaad:${jaadVersion}"
    }
}
